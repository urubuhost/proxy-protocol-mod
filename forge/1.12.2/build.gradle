buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net/' }
        mavenCentral()
        maven { url = 'https://repo.spongepowered.org/maven' }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:3.+'
        classpath 'org.spongepowered:mixingradle:0.7.+'
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '4.0.4'
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'

version = '1.0'
group = 'app.urubu.haproxy'
archivesBaseName = 'haproxy_protocol'

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

minecraft {
    mappings channel: 'snapshot', version: '20171003-1.12'

    runs {
        server {
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
        }
    }
}

repositories {
    mavenCentral()
}

configurations {
    shade
    implementation.extendsFrom shade
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.12.2-14.23.5.2859'

    implementation 'io.netty:netty-codec-haproxy:4.1.112.Final'

    shade('io.netty:netty-codec-haproxy:4.1.112.Final') {
        exclude group: '*', module: '*'
    }

    compile('org.spongepowered:mixin:0.8.5') {
        exclude module: 'launchwrapper'
        exclude module: 'guava'
        exclude module: 'commons-io'
        exclude module: 'gson'
    }

    compileOnly 'org.spongepowered:mixin:0.8.5:processor'
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
}

mixin {
    add sourceSets.main, "haproxy_protocol.refmap.json"
    config "haproxy_protocol.mixins.json"
}

// Fix the classpath the right way and once and for all
afterEvaluate {
    // Collect deps in runtimeClasspath but NOT in minecraft
    def deps = (configurations.runtimeClasspath - configurations.minecraft).join(';')
    // Add deps to classpath
    minecraft.runs.each { run ->
        run.token 'minecraft_classpath', deps
    }
}

jar {
    manifest {
        attributes([
            "Specification-Title": "haproxy_protocol",
            "Specification-Vendor": "NickUC",
            "Specification-Version": "1",
            "Implementation-Title": project.name,
            "Implementation-Version": "${version}",
            "Implementation-Vendor" :"NickUC",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
            "TweakClass" : "org.spongepowered.asm.launch.MixinTweaker"
        ])
    }
}

shadowJar {
    configurations = [project.configurations.shade]
    //archiveFileName = 'haproxy-protocol-forge-1.12.2.jar'

    def pkg = project.group
    def libPkg = pkg + '.lib.'
    relocate 'io.netty.handler.codec.haproxy', libPkg + 'haproxy'
}

reobf {
    shadowJar {}
}

assemble.dependsOn shadowJar

jar.finalizedBy('reobfJar')
